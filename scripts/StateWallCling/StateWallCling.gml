/// @desc StateWallSlide

/// PLAYER MOVEMENT INPUT

//calculate input direction
var _move	= keyRight - keyLeft; // -1 = left; 1 = right


//Correct leftover variables from previous state
momentumLock		= 0;
jumped				= false;
jumpBufferCounter	= 0;
coyoteCounter		= 0;
onGround			= 0;

hSpeed				= 0;
hSpeedFloat			= 0;

running				= false;
topSpeed			= Approach(topSpeed, speedWalk, runSlow);




//	╔═════════════════════╗▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬
//	║	HORIZONTAL MOVEMENT   ║
//	╚═════════════════════╝

facing = onWall;														//set player direction


// MOVING AWAY FROM WALL //////////////////////////////////////////////////////////////////////////////////////////////////////////

if (_move != 0 && _move != onWall)	{wallLeaveCounter ++;}				//if player is moving away from wall, add to counter
else if (wallLeaveCounter > 0) {wallLeaveCounter --;}					//if not, subtract from counter


if (wallLeaveCounter >= wallLeaveThreshold)								//if threshold is met
{
	onWall = 0;															//unstick from wall
	hSpeed = _move * 2;													//give the player a bit of movement speed away from wall
	facing = _move;
}


//▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬
//▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓
//▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓

//	╔═════════════════════╗▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬
//	║	 VERTICAL  MOVEMENT   ║
//	╚═════════════════════╝

// GRAVITY //////////////////////////////////////////////////////////////////////////////////////////////////////////
var _gravFinal;														//declare _grav as local variable
if (vSpeed < 0)	{_gravFinal = grav;}								//if player is moving upwards, apply regular gravity
else 			{_gravFinal = gravSlide;}							//set gravity to gravSlide

var _vSpeedMaxFinal = vSpeedMaxSlide;								//set max y speed to vspMaxSlide

vSpeed = Approach(vSpeed, _vSpeedMaxFinal, _gravFinal);				//apply gravity to vSpeed

// WALL JUMP ////////////////////////////////////////////////////////////////////////////////////////////////////////
if (keyJump)														//wall jump
{																	//
	hSpeed		= -facing*hspWallJump;								//set horizontal speed of wall jump
	vSpeed		= -vspWallJump;										//set vertical speed of wall jump
	jumped		= true;												//update jumped status
																	//
	hSpeedFloat = 0;												//drop horizontal sub-pixel speed
	vSpeedFloat	= 0;												//drop vertical sub-pixel speed
	momentumLock= momentumLockMax;									//set momentum lock timer to max
																	//
	facing		= -onWall;											//flip direction
																	//
	onWall		= false;											//player is not on wall
}



//▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬
//▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓
//▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓

//	╔═════════════════════╗▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬
//	║	  APPLY MOVEMENTS	  ║
//	╚═════════════════════╝

// Clamp Speed ////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
clamp(vSpeed,-_vSpeedMaxFinal, _vSpeedMaxFinal);
clamp(hSpeed,-hSpeedMax, hSpeedMax);

//Apply Movements + Collisions /////////////////////////////////////////////////////////////////////////////////////////////////////////
		//moved into scripts for organization
ApplyHoriz();
ApplyVertical();


//▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬
//▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓
//▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓

//	╔═════════════════════╗▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬
//	║		WALL SLIDING	  ║
//	╚═════════════════════╝

//Wall Slide
var _onWall = place_meeting(x+1,y,parSolid) - place_meeting(x-1,y,parSolid)			//if any part of player is touching a wall (-1 for left, 0 for none, 1 for right)

if (onWall == 0) _onWall = 0;														//if onWall was set to 0 earlier in the script, keep it at 0


//Check if bottom of target zone and top of target zone are both touching the same wall
	//the "target zone" is the area in in the middle of the player that must be touching the wall to slide
	//	The target zone is defined by its top and bottom (y values), and both these points 
	//must be touching the wall for the player to start sliding
	//	This means that the player will not slide against the wall if only the top or bottom is against the wall
											
//check if entire target zone is touching the wall
if (place_meeting(x+_onWall,bbox_bottom+wallTargetTop,parSolid) != 0)			//if at least one target zone point is touching the wall
{
	if (place_meeting(x+_onWall,bbox_bottom-wallTargetBottom,parSolid) == place_meeting(x+_onWall,bbox_top+wallTargetTop,parSolid))
	{
		onWall = _onWall;							//-1 means wall on left, 1 means wall on right, 0 means no wall
	}
	else {onWall = 0;}								//do not cling to wall
}
else {onWall = 0;}										//do not cling to wall

//▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬
//▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓
//▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓

//	╔═════════════════════╗▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬
//	║	CHANGE PLAYER STATE	  ║
//	╚═════════════════════╝

//RESET PLAYER STATE (IMPORTANT)
if (OnGround() == true)	playerState = StateFreePlatform;		//reset state if touching ground
if (onWall == 0)		playerState = StateFreePlatform;		//reset state if not on wall

if (playerState == StateFreePlatform)							//if new state is freeplatform
{																//
	wallLeaveCounter	= 0;									//reset wallLeaveCounter
	onWall				= 0;									//set onWall to 0 just in case it somehow wasn't already
	if (_move == 0 )	{facing = -facing}			//turn player around if neccessary
}

//▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬
//▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓
//▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓


//	╔═════════════════════╗▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬
//	║	UPDATE ANIMATION INFO ║
//	╚═════════════════════╝

// CALCULATE CURRENT STATE /////////////////////////////////////////////////////////////////////////////////////////////////////////
//Animations are still calculated for other states in case the player is no longer on a wall
//

image_speed = 1;																//reset animation speed to default
playerAction = "wall";															//default playerAction to wall

//Player is NOT on Ground
if (!onGround)																	//if NOT on ground
{																				//
	if (onWall != 0 )
	{
		playerAction	= "wall";												//update playerAction
		var _side				= bbox_left;									//default _side to left
		if (onWall == 1) _side	= bbox_right;									//change _side to right if needed
		  
		//Dust Effect
		dust++;																	//increment dust timer (spawn dust particle at 3)
		if ((dust > 2) && (vSpeed > 0)) with (instance_create_layer(_side,bbox_top+random_range(-2,2), "VFX1", objDust))	//create dust particle
		{																		//
			other.dust	= 0;													//reset player dust spawn timer
			hspeed		= -other.onWall * random_range(0.2,0.8);				//set dust direction opposite wall
		}
	}
	 else																		//if NOT on wall
	{																			//
		dust			=	0;													//reset dust spawn counter
		playerAction	= "air";												//player is in the air
	}
}
//Player is on Ground
else if (OnGround()) playerAction= "ground";									//if player ON ground


//Copy local variables to Sprite Mask
objPlayerMask.move	=	_move;